plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id "androidx.navigation.safeargs.kotlin"
    id 'maven-publish'
    id 'org.jetbrains.dokka'
}

group = "com.uploadcare.android.widget"

android {

    compileSdkVersion versions.compile_sdk

    defaultConfig {
        minSdkVersion versions.min_sdk
        targetSdkVersion versions.target_sdk
        versionCode 11
        versionName "${version}"
    }

    buildFeatures {
        dataBinding = true
    }

    androidExtensions {
        // Enable experimental Kotlin features in gradle to enable Parcelize
        // https://github.com/Kotlin/KEEP/blob/master/proposals/extensions/android-parcelable.md
        experimental = true
    }

    buildTypes {
        debug {
            debuggable true
            versionNameSuffix '-DEBUG'
            buildConfigField "String", "SOCIAL_API_ENDPOINT", '"https://social.uploadcare.com/"'
        }
        release {
            debuggable false
            minifyEnabled false
            buildConfigField "String", "SOCIAL_API_ENDPOINT", '"https://social.uploadcare.com/"'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility versions.jdk
        targetCompatibility versions.jdk
    }

    kotlinOptions {
        jvmTarget = versions.jdk
    }

    dokkaHtml.configure {
        dokkaSourceSets {
            named("main") {
                outputDirectory.set(buildDir.resolve("javadoc"))
                sourceRoots.setFrom(file("src/main/java"))
                noAndroidSdkLink.set(false)
                skipEmptyPackages.set(true)
                includeNonPublic.set(false)
            }
        }
    }
}

dependencies {
    implementation project (':library')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$versions.kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.kotlin_coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.kotlin_coroutines"

    implementation "androidx.core:core-ktx:$versions.core"

    implementation "androidx.navigation:navigation-fragment-ktx:$versions.navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$versions.navigation"

    implementation "androidx.fragment:fragment-ktx:$versions.fragment"
    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    implementation "com.google.android.material:material:$versions.material"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraintlayout"

    implementation "androidx.lifecycle:lifecycle-common-java8:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"

    implementation "androidx.work:work-runtime-ktx:$versions.work"
    implementation "androidx.work:work-gcm:$versions.work"

    implementation "com.squareup.moshi:moshi:$versions.moshi"
    implementation "com.squareup.moshi:moshi-adapters:$versions.moshi"
    implementation "com.squareup.moshi:moshi-kotlin:$versions.moshi"

    implementation "com.squareup.okhttp3:okhttp:$versions.okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttp"
    testImplementation "com.squareup.okhttp3:mockwebserver:$versions.okhttp"

    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"

    implementation "com.squareup.picasso:picasso:$versions.picasso"

    androidTestImplementation "androidx.test:runner:$versions.test_runner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.espresso_core"
}

// Make sure unit tests always run when we are building the code.
tasks.named("build") {
    dependsOn(tasks.named("check"))
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from(components["release"])

                groupId = project.group
                artifactId = "uploadcare-android-widget"
                version = project.version

                //withBuildIdentifier(), available in newer gradle versions.

                pom {
                    name.set("uploadcare-android-widget")
                    url.set("https://github.com/uploadcare/uploadcare-android")
                    issueManagement {
                        url.set("https://github.com/uploadcare/uploadcare-android/issues")
                    }
                    licenses {
                        license {
                            name.set("The Apache License, Version 2.0")
                            url.set("https://www.apache.org/licenses/LICENSE-2.0.txt")
                        }
                    }
                    scm {
                        connection.set("scm:git:git://github.com/uploadcare/uploadcare-android.git")
                        developerConnection.set("scm:git:ssh://github.com/uploadcare/uploadcare-android.git")
                        url.set("https://github.com/uploadcare/uploadcare-android")
                    }
                }
            }
        }
    }
}
